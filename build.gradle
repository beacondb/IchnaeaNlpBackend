/*
 * SPDX-FileCopyrightText: 2013, microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

/* This is a hack, trying to provide release updates to F-Droid until it can properly handle Gradle
applicationId "net.beacondb.unifiednlp"
versionName "1.7.0"
versionCode "20048"
 */

apply plugin: 'com.android.application'

String getMyVersionName() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec { commandLine 'git', 'describe', '--tags', '--always', '--dirty'
            standardOutput = stdout }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

int getMyVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', "HEAD"
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'org.microg.nlp:api:2.0-alpha9'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    defaultConfig {
        versionName getMyVersionName()
        versionCode(getMyVersionCode() + 20000)
        minSdkVersion 10
        targetSdkVersion 29
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    signingConfigs {
        release {
            storeFile file("release.jks")
            storePassword System.getenv("SIGNING_KEYSTORE_PASSWORD")
            keyAlias System.getenv("SIGNING_ALIAS")
            keyPassword System.getenv("SIGNING_ALIAS_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}
